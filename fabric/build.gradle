buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "fabricmc"
            url = "https://maven.fabricmc.net/"
        }
    }

    dependencies {
        classpath("net.fabricmc:fabric-loom:0.9.9")
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "fabric-loom"

archivesBaseName = "NorthernCompass-Fabric"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

minecraft {
    refmapName = "mixins.northerncompass.refmap.json"
}

configurations {
    shadow {
        runtimeOnly.extendsFrom(shadow)
    }
}

repositories {
    maven {
        name = "fabricmc"
        url = "https://maven.fabricmc.net/"
    }
}

dependencies {
    shadow(project(path: ":northerncompass-common"))
    minecraft("com.mojang:minecraft:1.17.1")
    mappings minecraft.officialMojangMappings()
    modImplementation("net.fabricmc:fabric-loader:0.11.6")
}

build {
    dependsOn("signJar")
}

compileJava {
    dependsOn(":northerncompass-common:build")
}

jar {
    enabled = false
    dependsOn("shadowJar")
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from (sourceSets.main.resources.srcDirs) {
        include("fabric.mod.json")
        expand("version": version)
    }
}

remapJar {
    dependsOn("shadowJar")
}

shadowJar {
    archiveClassifier.set("dev")
    configurations = [project.configurations.shadow]
}