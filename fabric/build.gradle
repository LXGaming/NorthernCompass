buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "fabricmc"
            url = "https://maven.fabricmc.net/"
        }
    }

    dependencies {
        classpath("net.fabricmc:fabric-loom:0.10.+")
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "fabric-loom"

archivesBaseName = "NorthernCompass-Fabric"

loom {
    mixin {
        defaultRefmapName = "mixins.northerncompass.refmap.json"
    }
}

configurations {
    compileJar {
        implementation.extendsFrom(compileJar)
    }
}

repositories {
    maven {
        name = "fabricmc"
        url = "https://maven.fabricmc.net/"
    }
}

dependencies {
    compileJar(project(path: ":northerncompass-common"))
    minecraft("com.mojang:minecraft:1.18.1")
    mappings loom.officialMojangMappings()
    modImplementation("net.fabricmc:fabric-loader:0.12.+")
}

build {
    dependsOn("signJar")
}

compileJava {
    dependsOn(":northerncompass-common:build")
}

jar {
    enabled = false
    dependsOn("shadowJar")
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from (sourceSets.main.resources.srcDirs) {
        include("fabric.mod.json")
        expand("version": version)
    }
}

remapJar {
    dependsOn("shadowJar")
}

shadowJar {
    archiveClassifier.set("dev")
    configurations = [project.configurations.compileJar]
}